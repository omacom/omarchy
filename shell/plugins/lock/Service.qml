import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Services.Pam
import Quickshell.Wayland
import qs.Commons

Item {
  id: root

  property var shell: null
  property string omarchyPath: ""

  readonly property string home: Quickshell.env("HOME")
  readonly property string stateHome: home + "/.local/state"
  readonly property string userName: Quickshell.env("USER") || Quickshell.env("LOGNAME")
  readonly property string currentBackgroundLink: stateHome + "/omarchy/current/background"
  readonly property string lockOwnerPath: Quickshell.env("XDG_RUNTIME_DIR") + "/omarchy-lock-owner-" + Quickshell.env("HYPRLAND_INSTANCE_SIGNATURE")

  property bool lockRequested: false
  property bool pendingSessionLock: false
  property bool authenticatingPassword: false
  property bool fingerprintAuthenticating: false
  property bool passwordPamConfigured: false
  property bool fingerprintConfigured: false
  property bool previewVisible: false
  property bool displayBlanked: false
  property bool displaysBlank: false
  property var monitorDpms: ({})
  property bool monitorDpmsKnown: false
  property int focusRequestVersion: 0
  property string enteredPassword: ""
  property string pendingPassword: ""
  property string failureMessage: ""
  property int failedAttempts: 0
  property string backgroundPath: ""
  property int backgroundVersion: 0
  property string lastEvent: "init"
  property string lastEventAt: ""
  property bool strandedLock: false
  property bool strandedLockResolved: false
  property bool lockOwnerReady: false
  property string lockOwnerInstance: ""
  property bool strandedRestartAttempted: false
  property bool wakePending: false
  property bool blankPending: false
  property bool cleanUnlockInProgress: false
  property int wakeRetryAttempt: 0
  readonly property int wakeRetryBudget: 3
  property int blankRetryAttempt: 0
  readonly property int blankRetryBudget: 3

  readonly property bool lockStatePoisoned: sessionLock.secure && !sessionLock.locked && !cleanUnlockInProgress
  readonly property bool locked: lockRequested || sessionLock.locked
  // This is the deterministic ownership signal used by the shell reload guard.
  // `secure` is deliberately excluded: after an in-process lock teardown it
  // reads through Quickshell's stale process-global session-lock pointer.
  readonly property bool sessionLockOwned: lockRequested || sessionLock.locked
  readonly property bool authenticating: authenticatingPassword || fingerprintAuthenticating
  readonly property bool videoBackground: Util.isVideoPath(backgroundPath)
  readonly property var batteryService: shell && shell.services ? shell.firstPartyServiceFor("omarchy.battery") : null
  readonly property bool powerSaverActive: batteryService ? batteryService.powerSaverOnBattery : false

  function realScreenCount() {
    var screens = Quickshell.screens || []
    var count = 0

    for (var i = 0; i < screens.length; i++) {
      var screen = screens[i]
      if (screen && screen.name && screen.width > 0 && screen.height > 0) count += 1
    }

    return count
  }

  function hasRealScreen() {
    return realScreenCount() > 0
  }

  function queueSessionLock() {
    pendingSessionLock = true
    if (!sessionLockStabilizeTimer.running) logEvent("lock-pending: screen-stabilizing")
    sessionLockStabilizeTimer.restart()
    if (!pendingSessionLockTimer.running) pendingSessionLockTimer.start()
  }

  function requestSessionLock() {
    if (lockStatePoisoned) {
      recoverPoisonedLockState()
      return
    }
    if (!lockRequested || sessionLock.locked || sessionLock.secure) return
    if (sessionLockStabilizeTimer.running) return

    if (!hasRealScreen()) {
      if (!pendingSessionLock || lastEvent !== "lock-pending: no-real-screen") logEvent("lock-pending: no-real-screen")
      pendingSessionLock = true
      if (!pendingSessionLockTimer.running) pendingSessionLockTimer.start()
      return
    }

    pendingSessionLock = false
    pendingSessionLockTimer.stop()
    sessionLock.locked = true
    if (sessionLock.locked) markSessionLockOwner()
  }

  // ext-session-lock outlives its client, and a restart carries no lock over, so
  // a session locked this early is an orphan behind Hyprland's failsafe. Outputs
  // are often still absent here, so ask until the answer means something.
  function checkStrandedLock() {
    if (strandedLockResolved || strandedLockCheckProc.running) return

    // A lock this shell took is nobody's orphan.
    if (sessionLockOwned) {
      strandedLockResolved = true
      return
    }

    strandedLockCheckProc.running = true
  }

  function recoverStrandedLock() {
    if (!strandedLock || sessionLockOwned || !passwordPamConfigured || !lockOwnerReady) return

    // A replacement service in the same Quickshell process cannot retake the
    // lock: destroying its predecessor leaves the process-global lock manager
    // poisoned. A detached restart gives recovery a clean manager and survives
    // the shell it is replacing. A genuinely fresh shell has a different
    // instance id and can safely take over the compositor's stranded lock.
    if (lockOwnerInstance === String(Quickshell.instanceId)) {
      restartForStrandedLock()
      return
    }

    strandedLock = false
    logEvent("lock-stranded: recovering")
    beginLock()
  }

  function restartForStrandedLock() {
    if (strandedRestartAttempted || sessionLock.locked) return

    strandedRestartAttempted = true
    sessionLockStabilizeTimer.stop()
    pendingSessionLockTimer.stop()
    logEvent("lock-stranded: restarting-poisoned-shell")
    Quickshell.execDetached(["omarchy-restart-shell"])
    strandedRestartRetryTimer.restart()
  }

  function recoverPoisonedLockState() {
    if (!lockStatePoisoned) return
    strandedLock = true
    strandedLockResolved = true
    restartForStrandedLock()
  }

  function markSessionLockOwner() {
    lockOwnerInstance = String(Quickshell.instanceId)
    lockOwnerFile.setText(lockOwnerInstance + "\n")
  }

  function clearSessionLockOwner() {
    lockOwnerInstance = ""
    lockOwnerFile.setText("")
  }

  function refreshBackground() {
    if (!readlinkProc.running) readlinkProc.running = true
  }

  function refreshFingerprintStatus() {
    if (!fingerprintCheckProc.running) fingerprintCheckProc.running = true
  }

  function logEvent(event) {
    lastEvent = event
    lastEventAt = new Date().toISOString()
    console.log("omarchy lock " + lastEventAt + " " + event)
  }

  function resetAuthenticationState() {
    enteredPassword = ""
    pendingPassword = ""
    failureMessage = ""
    failedAttempts = 0
    authenticatingPassword = false
    fingerprintAuthenticating = false
    fingerprintRetryTimer.stop()
    if (passwordPam.active) passwordPam.abort()
    if (fingerprintPam.active) fingerprintPam.abort()
  }

  function beginLock() {
    if (!passwordPamConfigured) {
      logEvent("lock-denied: missing-pam")
      return false
    }

    resetAuthenticationState()
    lockRequested = true
    armBlankTimer()
    logEvent("lock-requested")
    queueSessionLock()

    Qt.callLater(function() {
      root.refreshBackground()
      root.refreshFingerprintStatus()
    })

    return true
  }

  function finishUnlock() {
    if (!root.locked && !lockRequested) return

    lockRequested = false
    pendingSessionLock = false
    sessionLockStabilizeTimer.stop()
    pendingSessionLockTimer.stop()
    resetAuthenticationState()
    idleBlankTimer.stop()
    cleanUnlockInProgress = true
    sessionLock.locked = false
    clearSessionLockOwner()
    logEvent("unlocked")
    runWake()
  }

  function armBlankTimer() {
    idleBlankTimer.armedAt = Date.now()
    idleBlankTimer.restart()
  }

  function runWake() {
    root.displaysBlank = false
    displayBlanked = false
    monitorDpmsKnown = false
    focusRequestVersion += 1
    blankPending = false
    blankRetryTimer.stop()
    blankRetryAttempt = 0
    wakeRetryTimer.stop()
    wakeRetryAttempt = 0
    wakePending = true
    drainDisplayRequest()
    if (lockRequested) armBlankTimer()
  }

  function runBlank() {
    root.displaysBlank = true
    displayBlanked = true
    monitorDpmsKnown = false
    wakePending = false
    wakeRetryTimer.stop()
    wakeRetryAttempt = 0
    blankRetryTimer.stop()
    blankRetryAttempt = 0
    if (!blankProcess.running) blankPending = true
    drainDisplayRequest()
  }

  // Blank and wake are one ordered state machine. Each child is bounded, and
  // both exits drain the latest request, so a wedged process cannot suppress
  // display control forever or let a late DPMS-off win after a wake.
  function drainDisplayRequest() {
    if (blankProcess.running || wakeProcess.running) return
    if (wakePending) {
      wakePending = false
      wakeProcess.running = true
    } else if (blankPending) {
      blankPending = false
      blankProcess.running = true
    }
  }

  function handleWakeExit(exitCode) {
    if (exitCode === 0) {
      wakeRetryAttempt = 0
      wakeRetryTimer.stop()
      drainDisplayRequest()
      return
    }

    // A later blank supersedes this wake. Otherwise retry the latest wake even
    // after finishUnlock removed the lock surface and its input monitor.
    if (!displayBlanked && wakeRetryAttempt < wakeRetryBudget) {
      wakeRetryAttempt += 1
      wakePending = true
      wakeRetryTimer.interval = 250 * Math.pow(2, wakeRetryAttempt - 1)
      wakeRetryTimer.restart()
      return
    }

    wakePending = false
    drainDisplayRequest()
  }

  function handleBlankExit(exitCode) {
    if (exitCode === 0) {
      blankRetryAttempt = 0
      blankRetryTimer.stop()
      drainDisplayRequest()
      return
    }

    // Input or unlock may have requested a newer wake while blanking. Retry
    // only while this same lock still wants a blank display.
    if (lockRequested && displayBlanked && blankRetryAttempt < blankRetryBudget) {
      blankRetryAttempt += 1
      blankPending = true
      blankRetryTimer.interval = 250 * Math.pow(2, blankRetryAttempt - 1)
      blankRetryTimer.restart()
      return
    }

    blankPending = false
    drainDisplayRequest()
  }

  function screenBlank(screenName) {
    var name = String(screenName || "")
    if (!monitorDpmsKnown || !(name in monitorDpms)) return displaysBlank
    return !monitorDpms[name]
  }

  function applyMonitorDpms(text) {
    var monitors
    try {
      monitors = JSON.parse(String(text || ""))
    } catch (error) {
      return
    }
    if (!Array.isArray(monitors)) return

    var dpms = {}
    for (var i = 0; i < monitors.length; i++) {
      var monitor = monitors[i]
      if (monitor && monitor.name && !monitor.disabled) dpms[String(monitor.name)] = !!monitor.dpmsStatus
    }
    monitorDpms = dpms
    monitorDpmsKnown = true
  }

  function submitPassword(value) {
    var password = String(value || "")
    if (!lockRequested || authenticatingPassword || password.length === 0) return

    runWake()
    pendingPassword = password
    failureMessage = ""
    authenticatingPassword = true

    if (!passwordPam.start()) {
      handlePasswordFailure()
      return
    }

    Qt.callLater(respondToPasswordPrompt)
  }

  function respondToPasswordPrompt() {
    if (!authenticatingPassword || !passwordPam.active || !passwordPam.responseRequired) return
    passwordPam.respond(pendingPassword)
  }

  function handlePasswordFailure() {
    if (!lockRequested) return

    authenticatingPassword = false
    enteredPassword = ""
    pendingPassword = ""
    failedAttempts += 1
    failureMessage = "Authentication failed (" + failedAttempts + ")"
    runWake()
  }

  function startFingerprint() {
    if (!lockRequested || !sessionLock.secure || !fingerprintConfigured) return
    if (fingerprintPam.active || fingerprintAuthenticating) return

    fingerprintAuthenticating = true
    if (!fingerprintPam.start()) {
      fingerprintAuthenticating = false
    }
  }

  function handleFingerprintFinished(result) {
    fingerprintAuthenticating = false

    if (!lockRequested) return
    if (result === PamResult.Success) {
      finishUnlock()
    } else if (fingerprintConfigured) {
      fingerprintRetryTimer.restart()
    }
  }

  WlSessionLock {
    id: sessionLock

    locked: false

    onSecureStateChanged: {
      root.logEvent("secure=" + secure)
      if (!secure) {
        root.cleanUnlockInProgress = false
        if (root.lockRequested) root.queueSessionLock()
      }
      if (secure) {
        root.pendingSessionLock = false
        sessionLockStabilizeTimer.stop()
        pendingSessionLockTimer.stop()
        root.startFingerprint()
      }
    }

    onLockStateChanged: {
      root.logEvent("session-locked=" + locked)

      if (locked) {
        root.markSessionLockOwner()
        root.pendingSessionLock = false
        sessionLockStabilizeTimer.stop()
        pendingSessionLockTimer.stop()
      }

      if (!locked && root.lockRequested) {
        root.lockRequested = false
        root.pendingSessionLock = false
        sessionLockStabilizeTimer.stop()
        pendingSessionLockTimer.stop()
        root.resetAuthenticationState()
        root.runWake()
      }
    }

    WlSessionLockSurface {
      id: lockSurface
      color: Color.background

      LockView {
        id: lockView
        anchors.fill: parent
        backgroundPath: root.backgroundPath
        backgroundVersion: root.backgroundVersion
        fingerprintConfigured: root.fingerprintConfigured
        authenticatingPassword: root.authenticatingPassword
        failureMessage: root.failureMessage
        failedAttempts: root.failedAttempts
        inputEnabled: root.lockRequested
        loadBackground: root.locked
        displaysBlank: root.screenBlank(lockSurface.screen ? lockSurface.screen.name : "")
        powerSaverActive: root.powerSaverActive
        passwordText: root.enteredPassword
        displayBlanked: root.displayBlanked
        focusRequestVersion: root.focusRequestVersion
        onPasswordTextEdited: function(password) { root.enteredPassword = password }
        onSubmitPassword: function(password) { root.submitPassword(password) }
        onClearFailureRequested: root.failureMessage = ""
        onWakeRequested: root.runWake()
      }

    }
  }

  PanelWindow {
    id: previewWindow
    visible: root.previewVisible
    anchors { top: true; bottom: true; left: true; right: true }
    color: "transparent"
    WlrLayershell.namespace: "omarchy-lock-preview"
    WlrLayershell.layer: WlrLayer.Overlay
    WlrLayershell.keyboardFocus: WlrKeyboardFocus.Exclusive
    exclusionMode: ExclusionMode.Ignore

    LockView {
      anchors.fill: parent
      backgroundPath: root.backgroundPath
      backgroundVersion: root.backgroundVersion
      fingerprintConfigured: root.fingerprintConfigured
      authenticatingPassword: false
      failureMessage: ""
      failedAttempts: 0
      inputEnabled: false
      loadBackground: root.previewVisible
      powerSaverActive: root.powerSaverActive
      passwordText: ""
    }

    MouseArea {
      anchors.fill: parent
      acceptedButtons: Qt.LeftButton | Qt.RightButton
      onClicked: root.previewVisible = false
    }
  }

  PamContext {
    id: passwordPam
    config: "omarchy-lock-password"
    user: root.userName

    onResponseRequiredChanged: root.respondToPasswordPrompt()
    onPamMessage: root.respondToPasswordPrompt()

    onCompleted: function(result) {
      root.authenticatingPassword = false
      root.pendingPassword = ""

      if (!root.lockRequested) return
      if (result === PamResult.Success) root.finishUnlock()
      else root.handlePasswordFailure()
    }

    onError: function(error) {
      root.handlePasswordFailure()
    }
  }

  PamContext {
    id: fingerprintPam
    config: "omarchy-lock-fingerprint"
    user: root.userName

    onCompleted: function(result) {
      root.handleFingerprintFinished(result)
    }

    onError: function(error) {
      root.fingerprintAuthenticating = false
      if (root.lockRequested && root.fingerprintConfigured) fingerprintRetryTimer.restart()
    }
  }

  Timer {
    id: fingerprintRetryTimer
    interval: 250
    repeat: false
    onTriggered: root.startFingerprint()
  }

  Process {
    id: readlinkProc
    command: ["readlink", "-f", root.currentBackgroundLink]
    stdout: StdioCollector {
      waitForEnd: true
      onStreamFinished: {
        var next = String(text || "").trim()
        if (next !== root.backgroundPath) {
          root.backgroundPath = next
          root.backgroundVersion += 1
        }
      }
    }
  }

  Process {
    id: fingerprintCheckProc
    command: ["bash", "-c", "if [[ -f /etc/pam.d/omarchy-lock-fingerprint ]] && command -v fprintd-list >/dev/null 2>&1 && fprintd-list \"$USER\" 2>/dev/null | grep -qi finger; then echo yes; else echo no; fi"]
    stdout: StdioCollector { id: fingerprintCheckStdout; waitForEnd: true }
    onExited: {
      root.fingerprintConfigured = String(fingerprintCheckStdout.text || "").trim() === "yes"
      if (root.lockRequested && root.fingerprintConfigured) root.startFingerprint()
      else if (!root.fingerprintConfigured && fingerprintPam.active) fingerprintPam.abort()
    }
  }

  Process {
    id: strandedLockCheckProc
    command: ["bash", "-c", "omarchy-hyprland-session-locked"]
    onExited: function(exitCode) {
      // No output to read the lock off yet.
      if (exitCode === 2) return

      root.strandedLockResolved = true

      // A lock taken while this was in flight is this shell's own.
      root.strandedLock = exitCode === 0 && !root.sessionLockOwned
      root.recoverStrandedLock()
    }
  }

  Process {
    id: wakeProcess
    command: ["timeout", "--kill-after=0.2s", "2s", "bash", "-c", "omarchy-system-wake"]
    onExited: function(exitCode) { root.handleWakeExit(exitCode) }
  }

  Process {
    id: blankProcess
    command: ["timeout", "--kill-after=0.2s", "2s", "bash", "-c", "omarchy-brightness-keyboard off; omarchy-brightness-display off"]
    onExited: function(exitCode) { root.handleBlankExit(exitCode) }
  }

  Process {
    id: monitorDpmsProcess
    command: ["hyprctl", "monitors", "-j"]
    stdout: StdioCollector {
      onStreamFinished: root.applyMonitorDpms(text)
    }
  }

  Timer {
    id: monitorDpmsTimer
    interval: 3000
    repeat: true
    triggeredOnStart: true
    running: root.locked && root.videoBackground
    onTriggered: {
      if (!monitorDpmsProcess.running) monitorDpmsProcess.running = true
    }
    onRunningChanged: {
      if (!running) root.monitorDpmsKnown = false
    }
  }

  // Keyboard activity still reaches the compositor when no lock surface has
  // focus. Keep this armed for the whole lock so the first key after DPMS-off
  // can both wake the display and re-arm the bounded password-focus retry.
  IdleMonitor {
    enabled: root.lockRequested
    timeout: 1
    respectInhibitors: false
    onIsIdleChanged: {
      if (isIdle) return
      root.focusRequestVersion += 1
      if (root.displayBlanked) root.runWake()
    }
  }

  Timer {
    id: strandedRestartRetryTimer
    interval: 2000
    repeat: false
    onTriggered: {
      root.strandedRestartAttempted = false
      if (root.lockStatePoisoned || (root.strandedLock && root.lockOwnerInstance === String(Quickshell.instanceId)))
        root.restartForStrandedLock()
    }
  }

  Timer {
    id: wakeRetryTimer
    interval: 250
    repeat: false
    onTriggered: {
      if (!root.displayBlanked && root.wakePending) root.drainDisplayRequest()
    }
  }

  Timer {
    id: blankRetryTimer
    interval: 250
    repeat: false
    onTriggered: {
      if (root.lockRequested && root.displayBlanked && root.blankPending)
        root.drainDisplayRequest()
    }
  }

  Timer {
    id: idleBlankTimer
    interval: 5000
    repeat: false
    property double armedAt: 0
    onTriggered: {
      // A countdown frozen by suspend fires right after resume, which would
      // blank the freshly woken unlock screen under the user. Wall-clock time
      // exposes the gap: take a fresh run-up instead of blanking.
      if (Date.now() - armedAt > interval + 2000) {
        root.armBlankTimer()
        return
      }
      // Only a password check in flight should hold the display up. The
      // fingerprint PAM stays armed for the whole lock, so gating on
      // `authenticating` here would keep the panel lit until unlock.
      if (root.lockRequested && !root.authenticatingPassword) root.runBlank()
    }
  }

  Timer {
    id: sessionLockStabilizeTimer
    interval: 500
    repeat: false
    onTriggered: root.requestSessionLock()
  }

  Timer {
    id: pendingSessionLockTimer
    interval: 100
    repeat: true
    onTriggered: root.requestSessionLock()
  }

  Timer {
    id: strandedLockRetryTimer
    interval: 500
    repeat: true
    // Covers the compositor settling; screens coming back re-arm it.
    readonly property int budget: 20
    property int remaining: 20
    running: !root.strandedLockResolved && remaining > 0

    function rearm() {
      if (!root.strandedLockResolved) remaining = budget
    }

    onTriggered: {
      remaining -= 1
      root.checkStrandedLock()
    }
  }

  Connections {
    target: Quickshell
    function onScreensChanged() {
      root.displaysBlank = false
      root.requestSessionLock()
      if (root.lockRequested) root.focusRequestVersion += 1

      // A monitor still coming up has no workspace, so cannot answer yet.
      strandedLockRetryTimer.rearm()
      root.checkStrandedLock()
    }
  }

  onAuthenticatingPasswordChanged: {
    if (!lockRequested) return
    if (authenticatingPassword) idleBlankTimer.stop()
    else armBlankTimer()
  }

  FileView {
    id: lockOwnerFile
    path: root.lockOwnerPath
    atomicWrites: true
    blockWrites: true
    printErrors: false
    onLoaded: {
      root.lockOwnerInstance = String(text() || "").trim()
      root.lockOwnerReady = true
      root.recoverStrandedLock()
    }
    onLoadFailed: {
      root.lockOwnerInstance = ""
      root.lockOwnerReady = true
      root.recoverStrandedLock()
    }
  }

  FileView {
    path: "/etc/pam.d/omarchy-lock-password"
    watchChanges: true
    printErrors: false
    onLoaded: root.passwordPamConfigured = true
    onLoadFailed: root.passwordPamConfigured = false
    onFileChanged: reload()
  }

  // No lock before PAM is known good. An answer from before then may be stale --
  // the failsafe can be cleared from a TTY -- so re-ask rather than act on it.
  onPasswordPamConfiguredChanged: {
    if (!passwordPamConfigured) return

    strandedLock = false
    strandedLockResolved = false
    strandedLockRetryTimer.rearm()
    checkStrandedLock()
  }

  onLockStatePoisonedChanged: if (lockStatePoisoned) recoverPoisonedLockState()

  Component.onCompleted: {
    refreshBackground()
    refreshFingerprintStatus()
    checkStrandedLock()
    recoverPoisonedLockState()
  }

  IpcHandler {
    target: "lock"

    function lock(): string {
      if (!root.passwordPamConfigured) return "missing-pam"
      if (root.lockStatePoisoned) {
        root.recoverPoisonedLockState()
        return "recovering"
      }
      if (!root.locked && !root.beginLock()) return "failed"
      return "ok"
    }

    function isLocked(): string {
      return root.locked ? "true" : "false"
    }

    function status(): string {
      return JSON.stringify({
        locked: root.locked,
        requested: root.lockRequested,
        pending: root.pendingSessionLock,
        sessionLocked: sessionLock.locked,
        secure: sessionLock.secure,
        realScreens: root.realScreenCount(),
        passwordPam: root.passwordPamConfigured,
        fingerprint: root.fingerprintConfigured,
        authenticating: root.authenticating,
        lastEvent: root.lastEvent,
        lastEventAt: root.lastEventAt
      })
    }

    function preview(): string {
      root.refreshBackground()
      root.refreshFingerprintStatus()
      root.previewVisible = true
      return "ok"
    }

    function hidePreview(): string {
      root.previewVisible = false
      return "ok"
    }
  }
}
